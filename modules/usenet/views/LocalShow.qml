pragma ComponentBehavior: Bound

import QtQuick
import Components

FocusScope {
    id: showRoot

    property var navParams: ({})
    property var navListState: navParams.navListState || ({})

    signal navigateTo(string path, var params, var listState)
    signal goBack

    property var item: navParams.item || {}
    property string libraryName: navParams.libraryName || "LOCAL"
    property var rows: []
    property bool isLoading: false
    property string errorText: ""

    readonly property bool rowsAreEpisodes: rows.length > 0 && (rows[0].type || "") === "localFile"

    function numericValue(value, fallback) {
        var number = Number(value);
        return isFinite(number) ? number : (fallback || 0);
    }

    function hasResume(row) {
        return numericValue(row && row.viewOffset, 0) > 0 || numericValue(row && row.viewOffsetSeconds, 0) > 0;
    }

    function playEpisode(row) {
        if (!row)
            return;
        var episode = Object.assign({}, row);
        if (!episode.seriesTitle)
            episode.seriesTitle = item.title || "";
        navigateTo("LocalPlayer.qml", {
            item: episode,
            title: episode.title || "EPISODE"
        }, {
            currentIndex: childList.currentIndex
        });
    }

    function openRow(row) {
        if (!row)
            return;
        var type = String(row.type || "");
        if (type === "localFile") {
            playEpisode(row);
            return;
        }
        if (type === "localFolder") {
            navigateTo("LocalSeason.qml", {
                item: row,
                showTitle: item.title || "",
                libraryName: libraryName
            }, {
                currentIndex: childList.currentIndex
            });
            return;
        }
        errorText = "SEASON UNAVAILABLE";
    }

    function childLabel(row) {
        if (!row)
            return "";
        var title = row.title || "";
        if ((row.type || "") !== "localFile")
            return title;
        if (title.match(/^S\d+E\d+/i))
            return title;
        var path = row.path || "";
        var match = path.match(/s(\d+)[ ._-]*e(\d+)/i);
        if (match)
            return "S" + match[1] + "E" + match[2] + ": " + title;
        return title;
    }

    function showSubtitle() {
        var parts = [];
        if (rows.length > 0) {
            if (rowsAreEpisodes)
                parts.push(rows.length + (rows.length === 1 ? " EPISODE" : " EPISODES"));
            else
                parts.push(rows.length + (rows.length === 1 ? " SEASON" : " SEASONS"));
        }
        if ((item.path || "") !== "")
            parts.push(item.path);
        return parts.join(" - ");
    }

    Connections {
        target: usenetBackend

        function onItemsLoaded(categoryTitle, loadedRows) {
            showRoot.isLoading = false;
            showRoot.rows = loadedRows || [];
            showRoot.errorText = showRoot.rows.length === 0 ? "NO SEASONS FOUND" : "";
            if (showRoot.rows.length > 0) {
                var restore = (navListState.currentIndex !== undefined) ? navListState.currentIndex : 0;
                childList.currentIndex = Math.min(restore, showRoot.rows.length - 1);
                childList.positionViewAtIndex(childList.currentIndex, ListView.Contain);
            }
        }

        function onErrorOccurred(message) {
            showRoot.isLoading = false;
            showRoot.errorText = message || "SERIES LOAD FAILED";
        }
    }

    Component.onCompleted: {
        isLoading = true;
        errorText = "";
        usenetBackend.load_local_items(item.categoryId || "", item.path || "", item.sourceIndex || 0, item.title || "Show");
    }

    focus: true

    Keys.onUpPressed: {
        if (childList.currentIndex > 0)
            childList.currentIndex--;
    }
    Keys.onDownPressed: {
        if (childList.currentIndex < rows.length - 1)
            childList.currentIndex++;
    }
    Keys.onLeftPressed: {
        if (childList.count > 0) {
            var pageRows = Math.max(1, Math.floor(childList.height / (root.sh * 0.0583333)) - 1);
            childList.currentIndex = Math.max(0, childList.currentIndex - pageRows);
            childList.positionViewAtIndex(childList.currentIndex, ListView.Contain);
        }
    }
    Keys.onRightPressed: {
        if (childList.count > 0) {
            var pageRows = Math.max(1, Math.floor(childList.height / (root.sh * 0.0583333)) - 1);
            childList.currentIndex = Math.min(childList.count - 1, childList.currentIndex + pageRows);
            childList.positionViewAtIndex(childList.currentIndex, ListView.Contain);
        }
    }
    Keys.onReturnPressed: {
        openRow(rows[childList.currentIndex]);
    }
    Keys.onPressed: function (event) {
        if (event.key === Qt.Key_Escape || event.key === Qt.Key_Backspace || event.key === Qt.Key_Back) {
            goBack();
            event.accepted = true;
        }
    }

    StaticBackground {
        anchors.fill: parent
        visible: root.staticBackgroundEnabled
        running: visible
    }

    Rectangle {
        anchors.fill: parent
        color: root.staticBackgroundEnabled ? "transparent" : root.surfaceColor
    }

    AppBar {
        iconSource: moduleRoot.moduleIcon
        iconHeight: root.sh * 0.075
        title: moduleRoot.moduleName
        subtitle: libraryName
        anchors.top: parent.top
        anchors.left: parent.left
        anchors.topMargin: root.sh * 0.125
        anchors.leftMargin: root.sw * 0.125
    }

    Text {
        visible: isLoading
        text: "LOADING..."
        color: root.tertiaryColor
        font.family: root.globalFont
        anchors.centerIn: parent
        font.pixelSize: root.sh * 0.05
    }

    Item {
        visible: !isLoading
        anchors.top: parent.top
        anchors.left: parent.left
        anchors.topMargin: root.sh * 0.25
        anchors.leftMargin: root.sw * 0.115625
        width: root.sw * 0.76875
        height: root.sh * 0.525
        clip: true

        Row {
            id: showDetails
            height: root.sh * 0.2416667
            spacing: root.sw * 0.0375

            Rectangle {
                color: root.surfaceColor
                border.color: root.accentColor
                width: root.sw * 0.1875
                height: root.sh * 0.1166667
                border.width: root.sh * 0.003125

                Text {
                    anchors.centerIn: parent
                    text: "SERIES"
                    color: root.primaryColor
                    font.family: root.globalFont
                    font.pixelSize: root.sh * 0.05
                }
            }

            Column {
                topPadding: root.sh * 0.0083333
                width: root.sw * 0.54375
                spacing: root.sh * 0.0166667

                Text {
                    text: item.title || ""
                    color: root.primaryColor
                    font.family: root.globalFont
                    font.capitalization: Font.AllUppercase
                    elide: Text.ElideRight
                    width: parent.width
                    font.pixelSize: root.sh * 0.05
                }

                Text {
                    text: showSubtitle()
                    color: root.secondaryColor
                    font.family: root.globalFont
                    font.capitalization: Font.AllUppercase
                    elide: Text.ElideRight
                    width: parent.width
                    font.pixelSize: root.sh * 0.0333333
                }

                Item {
                    anchors.left: parent.left
                    anchors.right: parent.right
                    height: root.sh * 0.1375
                    clip: true

                    Text {
                        anchors.left: parent.left
                        anchors.right: parent.right
                        text: rowsAreEpisodes ? "SERVER LOCAL EPISODE INDEX" : "SERVER LOCAL SEASON INDEX"
                        color: root.primaryColor
                        font.family: root.globalFont
                        wrapMode: Text.WordWrap
                        font.pixelSize: root.sh * 0.0291667
                        lineHeight: 1.3
                    }
                }
            }
        }

        Text {
            id: showError
            visible: errorText !== ""
            anchors.top: showDetails.bottom
            text: errorText
            color: root.secondaryColor
            font.family: root.globalFont
            font.capitalization: Font.AllUppercase
            leftPadding: root.sw * 0.009375
            font.pixelSize: root.sh * 0.0291667
        }

        Text {
            id: childListLabel
            visible: rows.length > 0
            anchors.top: showError.visible ? showError.bottom : showDetails.bottom
            text: rowsAreEpisodes ? "Episodes:" : "Seasons:"
            color: root.secondaryColor
            font.family: root.globalFont
            font.capitalization: Font.AllUppercase
            anchors.topMargin: root.sh * 0.0145833
            leftPadding: root.sw * 0.009375
            rightPadding: root.sw * 0.009375
            font.pixelSize: root.sh * 0.0291667
        }

        ListView {
            id: childList
            visible: rows.length > 0
            model: rows
            anchors.top: childListLabel.bottom
            anchors.left: parent.left
            anchors.right: parent.right
            anchors.topMargin: root.sh * 0.0145833
            height: root.sh * 0.175
            clip: true

            delegate: ScrollingRow {
                required property var modelData
                required property int index

                list: childList
                rowIndex: index
                focused: true
                text: showRoot.childLabel(modelData)
                detail: (modelData.type || "") === "localFolder" ? "OPEN" : (showRoot.hasResume(modelData) ? "RSUM" : (modelData.durationDisplay || modelData.sizeText || ""))
            }
        }
    }

    component ScrollingRow: Item {
        id: rowRoot

        property var list
        property int rowIndex: -1
        property bool focused: true
        property string text: ""
        property string detail: ""
        readonly property bool selected: list.currentIndex === rowIndex && focused

        width: list.width
        height: root.sh * 0.0583333

        Item {
            id: textClip
            width: Math.min(rowText.implicitWidth + detailText.width + root.sw * 0.035, parent.width)
            height: parent.height
            clip: true

            Rectangle {
                color: root.accentColor
                anchors.fill: rowText
                visible: selected
            }

            Text {
                id: rowText
                text: rowRoot.text
                color: rowRoot.selected ? root.surfaceColor : root.primaryColor
                font.family: root.globalFont
                font.capitalization: Font.AllUppercase
                anchors.verticalCenter: parent.verticalCenter
                x: 0
                topPadding: root.sh * 0.0041667
                leftPadding: root.sw * 0.009375
                rightPadding: root.sw * 0.009375
                bottomPadding: root.sh * 0.00625
                font.pixelSize: root.sh * 0.05
            }

            SequentialAnimation {
                running: rowRoot.selected && rowText.implicitWidth > textClip.width
                loops: Animation.Infinite
                onRunningChanged: if (!running)
                    rowText.x = 0
                PauseAnimation {
                    duration: 1500
                }
                NumberAnimation {
                    target: rowText
                    property: "x"
                    to: textClip.width - rowText.implicitWidth
                    duration: Math.abs(to) * 20
                }
                PauseAnimation {
                    duration: 2000
                }
                PropertyAction {
                    target: rowText
                    property: "x"
                    value: 0
                }
            }
        }

        Text {
            id: detailText
            visible: rowRoot.detail !== ""
            text: rowRoot.detail
            color: rowRoot.selected ? root.secondaryColor : root.tertiaryColor
            font.family: root.globalFont
            font.capitalization: Font.AllUppercase
            anchors.verticalCenter: parent.verticalCenter
            anchors.right: parent.right
            anchors.rightMargin: root.sw * 0.009375
            horizontalAlignment: Text.AlignRight
            width: root.sw * 0.16
            elide: Text.ElideRight
            font.pixelSize: root.sh * 0.032
        }
    }
}
